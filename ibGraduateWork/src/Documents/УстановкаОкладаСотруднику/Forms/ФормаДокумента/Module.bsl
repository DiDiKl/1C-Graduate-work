&НаКлиенте
Перем МсСпискиДолжностей;  

&НаКлиенте
Процедура ПолучитьСписокДолжностей()
	Если МсСпискиДолжностей = Неопределено Тогда
		Если ЗначениеЗаполнено(Объект.Сотрудник) Тогда
			МсСпискиДолжностей = СервисВызовСервера.ПолучитьСписокДолжностей(Объект.Сотрудник, Истина);
		Иначе	
		    МсСпискиДолжностей = Новый Массив(2);
			МсСпискиДолжностей[1] = СервисВызовСервера.ПолучитьСписокДолжностей();
		КонецЕсли;
	Иначе
		МсСпискиДолжностей[0] = ?(ЗначениеЗаполнено(Объект.Сотрудник), СервисВызовСервера.ПолучитьСписокДолжностей(Объект.Сотрудник), Неопределено);
	КонецЕсли;
	Если МсСпискиДолжностей[0] <> Неопределено И МсСпискиДолжностей[0].Количество() Тогда
		Элементы.РежимСпискаДолжностей.ТолькоПросмотр = Ложь;
		РежимСпискаДолжностей = 0;
	Иначе
		Элементы.РежимСпискаДолжностей.ТолькоПросмотр = Истина;
		РежимСпискаДолжностей = 1;
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура СформироватьСписокВыбораДолжностей()
	СписокВыбора = Элементы.Должность.СписокВыбора;
	СписокВыбора.Очистить();
	МсДолжности = МсСпискиДолжностей[РежимСпискаДолжностей];
	Если МсДолжности = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СписокВыбора.ЗагрузитьЗначения(МсДолжности);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПолучитьСписокДолжностей();
	СформироватьСписокВыбораДолжностей();
КонецПроцедуры

&НаКлиенте
Процедура СотрудникОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	Если Объект.Сотрудник = ВыбранноеЗначение Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	ПолучитьСписокДолжностей();
	СформироватьСписокВыбораДолжностей();
КонецПроцедуры

&НаКлиенте
Процедура РежимСпискаДолжностейПриИзменении(Элемент)
	СформироватьСписокВыбораДолжностей();	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ПолучитьСписокДолжностей();
	СформироватьСписокВыбораДолжностей();
КонецПроцедуры




