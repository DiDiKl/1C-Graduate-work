
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ДокументЗакупкиПродажиСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	КоэффПеревода = СервисВызовСервера.ПолучитьКоэффициентПереводаВалютыВУчетнуюВалюту(Валюта, Дата, Истина, Отказ);	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;       
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	ПоступлениеТоваровТовары.Номенклатура.ВидНоменклатуры.Товар КАК ВидНоменклатурыТовар,
		|	СУММА(ПоступлениеТоваровТовары.Количество) КАК Количество,
		|	СУММА(ПоступлениеТоваровТовары.Стоимость) КАК Стоимость
		|ИЗ
		|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
		|ГДЕ
		|	ПоступлениеТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровТовары.Номенклатура,
		|	ПоступлениеТоваровТовары.Номенклатура.ВидНоменклатуры.Товар";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	Пока Выборка.Следующий() Цикл
		Если Выборка.ВидНоменклатурыТовар Тогда
			СтоимостьВВалУчет = Выборка.Стоимость * КоэффПеревода;
			
			Движение = Движения.ОстаткиТоваров.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Склад = Склад;
			Движение.Партия = Ссылка;
			Движение.Количество = Выборка.Количество;
			Движение.Стоимость = СтоимостьВВалУчет;
			
			Движение = Движения.Хозрасчетный.Добавить();
			Движение.СчетДт = ПланыСчетов.Хозрасчетный.ТоварыНаСкладах;
			Движение.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
			Движение.Период = Дата;
			Движение.КоличествоДт = Выборка.Количество;
			Движение.Сумма = СтоимостьВВалУчет;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склад] = Склад;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Партия] = Ссылка;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Контрагент] = Контрагент;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Договор] = Договор;
		КонецЕсли;
	КонецЦикла;
    //{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры
