
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	ЕСТЬNULL(Штрхкоды.Штрихкод, """") КАК Штрихкод
		|ИЗ
		|	Справочник.Номенклатура КАК СпрНоменклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрхкоды КАК Штрхкоды
		|		ПО СпрНоменклатура.Ссылка = Штрхкоды.Номенклатура
		|ГДЕ
		|	НЕ СпрНоменклатура.ЭтоГруппа";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Список = Объект.СписокНоменклатуры;
	Пока Выборка.Следующий() Цикл
		СтрокаНов = Список.Добавить();
		СтрокаНов.Номенклатура 	 = Выборка.Номенклатура;
		СтрокаНов.Штрихкод 		 = Выборка.Штрихкод;
	КонецЦикла;  
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	ТабШК = Новый ТаблицаЗначений;
	ТабШК.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТабШК.Колонки.Добавить("Штрихкод", Новый ОписаниеТипов("Строка"));
	Для каждого СтрокаСписка Из Объект.СписокНоменклатуры Цикл
		Если СтрокаСписка.Штрихкод <> "" Тогда
			СтрокаТабШК = ТабШК.Добавить();	
			СтрокаТабШК.Номенклатура = СтрокаСписка.Номенклатура;
			СтрокаТабШК.Штрихкод = СтрокаСписка.Штрихкод;
		КонецЕсли;		
	КонецЦикла;
	ЗаписиШК = РегистрыСведений.Штрхкоды.СоздатьНаборЗаписей();
	ЗаписиШК.Записывать = Истина;
	ЗаписиШК.Загрузить(ТабШК);
	ЗаписиШК.Записать();
КонецПроцедуры

&НаКлиенте
Процедура СгенерироватьКоды(Команда)
	ГСЧ = Новый ГенераторСлучайныхЧисел(ТекущаяУниверсальнаяДатаВМиллисекундах());
	ШтрихкодНач = СтрЗаменить(СокрП(ШтрихкодНачальный), " ", "0");
	Код0 = КодСимвола("0", 1);
	Код9 = КодСимвола("9", 1);
	ТолькоПустые = РежимГенерации = 0;
	Для каждого СтрокаСписка Из Объект.СписокНоменклатуры Цикл
		Если ТолькоПустые И СтрокаСписка.Штрихкод <> "" Тогда
			Продолжить;	
		КонецЕсли; 
		Штрихкод = ШтрихкодНач;
		Для Счет = СтрДлина(Штрихкод)+1 По 13 Цикл
			КодСимвол = ГСЧ.СлучайноеЧисло(Код0, Код9);
			Штрихкод = Штрихкод + Символ(КодСимвол);			
		КонецЦикла;
		СтрокаСписка.Штрихкод = Штрихкод; 
	КонецЦикла;
КонецПроцедуры
