
#Область Служебные_Экспорт

Функция Представление(Документ) Экспорт
	Если Документ = Null Тогда			
		Возврат "";
	КонецЕсли;
	ТипДок = ТипЗнч(Документ);
	НомерЧис = Число(Документ.Номер);
	Если ТипДок = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		Возврат "Заказ поставщику № " + НомерЧис;
	ИначеЕсли ТипДок = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		Возврат "Заказ клиента № " + НомерЧис;	
	ИначеЕсли ТипДок = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		Возврат "Поступление № " + НомерЧис;
	ИначеЕсли ТипДок = Тип("ДокументСсылка.РеализацияТоваров") Тогда
		Возврат "Реализация № " + НомерЧис;	
	КонецЕсли;
    Возврат "Документ №" + НомерЧис;
КонецФункции // Представление()

#КонецОбласти

#Область Служебные_НеЭкспорт

Процедура УстановитьДоступностьКнопкиОбновленияЦен(ФормаДокумента, Доступность)
	КнопкаОЦ = ФормаДокумента.Элементы.ТоварыОбновитьЦены;
	КнопкаОЦ.Доступность = Доступность;
	КнопкаОЦ.ЦветФона = ?(Доступность, Новый Цвет(150, 220, 0), Новый Цвет(255, 255, 255));
КонецПроцедуры

// Процедура - Расчитать итоговую сумму
// Расчитывает итоговую сумму.
// Параметры:
//  Документ - ДанныеФормыСтруктура, ДокументОбъект - Основной реквизит формы документа или объект документа. 
Процедура РасчитатьИтоговуюСумму(Документ)
    Документ.ИтоговаяСумма = Документ.Товары.Итог("Стоимость");
КонецПроцедуры

Процедура РасчитатьСтоимостьТабЧасть(СтрокаТЧ)
	СтрокаТЧ.Стоимость = СтрокаТЧ.Цена * СтрокаТЧ.Количество;
КонецПроцедуры

Процедура ОбновитьЦенуНоменклатурыРасчитатьСтоимостьТабЧасть(СтрокаТЧ, ОбъектФормы) 
	Если ЗначениеЗаполнено(ОбъектФормы.ВидЦены) Тогда
		СтрокаТЧ.Цена = СервисВызовСервера.ПолучитьЦенуНоменклатуры(СтрокаТЧ.Номенклатура, ОбъектФормы.ВидЦены, ОбъектФормы.Валюта, ОбъектФормы.Дата);	
		РасчитатьСтоимостьТабЧасть(СтрокаТЧ);
	КонецЕсли;
КонецПроцедуры 

// Процедура - Расчитать
// Расчитывает "Стоимость" текущей строки и "Итоговую сумму". Преднозначен для использования в процедурах-обработчиках событий элементов формы. 
// Параметры:
//  ФормаДокумента - ФормаКлиентскогоПриложения - Форма документа значения полей которого нужно пересчитать.
//  ОбновитьЦенуНоменклатуры - Булево - Обновить цену номенклатуры по заданному "Виду цены".
//  ВсеСтроки - Булево - Если Истина будет обновлена цена номенклатуры в каждой строке с перерасчетом Стоимости.
Процедура РасчитатьФорма(ФормаДокумента, ОбновитьЦенуНоменклатуры=Ложь, ВсеСтроки=Ложь)
	ОбъектФормы = ФормаДокумента.Объект;
	Если ОбъектФормы.Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Если ОбновитьЦенуНоменклатуры Тогда 
		Если ВсеСтроки Тогда
			МсНоменклатуры = Новый Массив();
			Для каждого Товар Из ОбъектФормы.Товары Цикл
				МсНоменклатуры.Добавить(Товар.Номенклатура);
			КонецЦикла;
			МсЦеныНоменк = СервисВызовСервера.ПолучитьЦеныНоменклатур(МсНоменклатуры, ОбъектФормы.ВидЦены, ОбъектФормы.Валюта, ОбъектФормы.Дата);
			Для Инд = 0 По МсНоменклатуры.ВГраница() Цикл
				Товар = ОбъектФормы.Товары[Инд];
				Товар.Цена = МсЦеныНоменк[Инд];
				РасчитатьСтоимостьТабЧасть(Товар);
			КонецЦикла;
		Иначе
			ОбновитьЦенуНоменклатурыРасчитатьСтоимостьТабЧасть(ФормаДокумента.Элементы.Товары.ТекущиеДанные, ОбъектФормы);
		КонецЕсли;		
	Иначе
		РасчитатьСтоимостьТабЧасть(ФормаДокумента.Элементы.Товары.ТекущиеДанные);	
	КонецЕсли;  
	ОбъектФормы.ИтоговаяСумма = ОбъектФормы.Товары.Итог("Стоимость");
КонецПроцедуры 

#КонецОбласти 

#Область События 

Процедура ПриОткрытии(ФормаДокумента) Экспорт
	УстановитьДоступностьКнопкиОбновленияЦен(ФормаДокумента, Ложь);
КонецПроцедуры

Процедура ВидЦеныПриИзменении(ФормаДокумента) Экспорт
	УстановитьДоступностьКнопкиОбновленияЦен(ФормаДокумента, Истина);
КонецПроцедуры  

Процедура ТоварыНоменклатураПриИзменении(ФормаДокумента) Экспорт
	РасчитатьФорма(ФормаДокумента, Истина); 
КонецПроцедуры

Процедура ТоварыРасчитатьСтоимость(ФормаДокумента) Экспорт
	РасчитатьФорма(ФормаДокумента);
КонецПроцедуры	

Процедура ТоварыПослеУдаления(ФормаДокумента) Экспорт
	РасчитатьИтоговуюСумму(ФормаДокумента.Объект);
КонецПроцедуры 

Процедура ВалютаОбработкаВыбора(ФормаДокумента, ВыбранноеЗначение) Экспорт
	Если ФормаДокумента.Объект.Валюта <> ВыбранноеЗначение Тогда
		УстановитьДоступностьКнопкиОбновленияЦен(ФормаДокумента, Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#Область Команды

Процедура ОбновитьЦены(ФормаДокумента) Экспорт
	РасчитатьФорма(ФормаДокумента, Истина, Истина);
	УстановитьДоступностьКнопкиОбновленияЦен(ФормаДокумента, Ложь);
КонецПроцедуры

#КонецОбласти  
