
Функция ПолучитьКатегорию(Документ)
	ТипДок = ТипЗнч(Документ.Ссылка);	
	Если ТипДок = Тип("ДокументСсылка.ЗаказПоставщику") Или ТипДок = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		Возврат "Закупка";
	ИначеЕсли ТипДок = Тип("ДокументСсылка.ЗаказКлиента") Или ТипДок = Тип("ДокументСсылка.РеализацияТоваров") Тогда
		Возврат "Продажа";	
	КонецЕсли;
    Возврат "Нет";
КонецФункции // Получить()

// Процедура - Заполнить пустые поля шапки на сервере
// Заполняет пустые поля объекта или формы переданного документа "Вид цены", "Валюта", "Склад" значениями поумолчанию, установленными для констант 
// ("Основной вид цены закупки/продажи", "Основная валюта", "Склад поступления/реализации поумолчанию").
// Параметры:
//  Документ - ДанныеФормыСтруктура, ДокументОбъект - Основной реквизит формы документа или объект документа.
//  ПроверитьЭтоНовый - Булево - Если истина, зполнение будет только если документ новый.
Процедура ЗаполнитьПустыеПоляШапки(Документ)
	Категория = ПолучитьКатегорию(Документ);
	Если Категория = "Нет" Тогда
		Возврат;	
	КонецЕсли;
	Закупка = Категория = "Закупка";
	Если Не ЗначениеЗаполнено(Документ.ВидЦены) Тогда
		Документ.ВидЦены = ?(Закупка, Константы.ОсновнойВидЦеныЗакупки.Получить(), Константы.ОсновнойВидЦеныПродажи.Получить());	
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Документ.Валюта) Тогда
		Документ.Валюта = Константы.ОсновнаяВалюта.Получить();	
	КонецЕсли;
    Если Не ЗначениеЗаполнено(Документ.Склад) Тогда
		Документ.Склад = ?(Закупка, Константы.СкладПоступленияПоумолчанию.Получить(), Константы.СкладРеализацииПоумолчанию.Получить());	
	КонецЕсли;
КонецПроцедуры

// Функция - Заполнить на основе на сервере
// Заполняет документ поступления или реализации товаров на основе документов заказа, или значениями по умолчанию если документ-основа не указан.
// Параметры:
//  Документ		 - ДокументОбъект - Документ который нужно заполнить.
//  ДокументОснова	 - ДокументСсылка - Документ с данныим заполнения.
Процедура Заполнить(Документ, ДокументОснова = Неопределено) Экспорт 	
	Если ДокументОснова = Неопределено Тогда
	    ЗаполнитьПустыеПоляШапки(Документ);
	Иначе
		ТипДок = ТипЗнч(ДокументОснова);
		Если ТипДок = Тип("ДокументСсылка.ЗаказПоставщику") Или ТипДок = Тип("ДокументСсылка.ЗаказКлиента") Тогда
			Документ.Валюта = ДокументОснова.Валюта;
			Документ.ВидЦены = ДокументОснова.ВидЦены;
			Документ.Договор = ДокументОснова.Договор;
			Документ.ИтоговаяСумма = ДокументОснова.ИтоговаяСумма;
			Документ.Контрагент = ДокументОснова.Контрагент;
			Документ.Склад = ДокументОснова.Склад;
			Для Каждого ТекСтрокаТовары Из ДокументОснова.Товары Цикл
				ЗаполнитьЗначенияСвойств(Документ.Товары.Добавить(), ТекСтрокаТовары);
			КонецЦикла;
		Иначе
			ЗаполнитьПустыеПоляШапки(Документ);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры